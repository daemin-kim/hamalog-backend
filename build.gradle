plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'org.owasp.dependencycheck' version '8.4.3'
	// Kotlin 플러그인
	id 'org.jetbrains.kotlin.jvm' version '2.0.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21'
	// Gatling 성능 테스트
	id 'io.gatling.gradle' version '3.11.5'
}

spotless {
	java {
		removeUnusedImports()
		importOrder()
		// 빈 줄 주석 제거 (예: "//", "// ")
		replaceRegex 'Remove empty comments', '//\\s*\n', '\n'
		// 연속 빈 줄을 하나로 정리
		replaceRegex 'Remove multiple blank lines', '\n{3,}', '\n\n'
		target 'src/**/*.java'
	}
	// Kotlin 코드 포맷팅 (ktlint 기반)
	kotlin {
		target 'src/**/*.kt'
		ktlint('1.5.0')
			.editorConfigOverride([
				'ktlint_standard_no-wildcard-imports': 'disabled',
				'ktlint_standard_package-name': 'disabled',  // com.Hamalog 패키지명 허용
				'max_line_length': '120'
			])
	}
	kotlinGradle {
		target '*.gradle.kts'
		ktlint('1.5.0')
	}
}


group = 'com'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

// Kotlin 컴파일 옵션 설정
kotlin {
	compilerOptions {
		freeCompilerArgs.addAll('-Xjsr305=strict')  // null 안전성 강화
		jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}



dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// Micrometer Prometheus registry for metrics scraping
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// Kotlin 표준 라이브러리
	implementation 'org.jetbrains.kotlin:kotlin-stdlib'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	// Jackson Kotlin 모듈 (JSON 직렬화/역직렬화)
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

	// Flyway - 데이터베이스 마이그레이션
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'

	// QueryDSL - 타입 안전한 동적 쿼리 생성
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'


	implementation 'org.springframework.boot:spring-boot-starter-webflux' // For WebClient functionality
	
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// ArchUnit - 아키텍처 테스트
	testImplementation 'com.tngtech.archunit:archunit-junit5:1.2.1'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.apache.httpcomponents.client5:httpclient5'

	// Firebase Admin SDK - FCM Push 알림
	implementation 'com.google.firebase:firebase-admin:9.2.0'

	testImplementation 'org.apache.httpcomponents.client5:httpclient5'
}


tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		csv.required = true
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}

jacoco {
	toolVersion = "0.8.10"
}

// QueryDSL Q클래스 생성 경로 설정
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
	main.java.srcDirs += querydslDir
	// Kotlin 소스 디렉토리 설정
	main.kotlin.srcDirs += ['src/main/kotlin']
	test.kotlin.srcDirs += ['src/test/kotlin']
}

// Kotlin 컴파일이 Java보다 먼저 실행되도록 설정 (Java-Kotlin 상호운용)
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	dependsOn tasks.named('processResources')
}

tasks.withType(JavaCompile).configureEach {
	options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean.doLast {
	file(querydslDir).deleteDir()
}

// OWASP Dependency Check 설정
dependencyCheck {
	// 취약점 발견 시 빌드 실패 기준 (CVSS 점수, 7.0 이상이면 실패)
	failBuildOnCVSS = 7.0f

	// 검사 대상 설정
	analyzers {
		nodeEnabled = false
		nodeAuditEnabled = false
	}

	// 리포트 설정
	formats = ['HTML', 'JSON']
	outputDirectory = layout.buildDirectory.dir('reports/dependency-check').get().asFile.path
}

// Gatling 성능 테스트 설정
gatling {
    // Kotlin 시뮬레이션 소스 디렉토리 설정
    simulationsDir = file('src/gatling/kotlin')
}
